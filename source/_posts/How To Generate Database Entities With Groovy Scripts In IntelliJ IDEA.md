---
title: How To Generate Database Entities With Groovy Scripts In IntelliJ IDEA
sub_title: idea-database-groovy
cover: https://uposs.justokay.cn/images/idea/idea.png
date: 2022-11-12 22:14:07
categories: IntelliJ IDEA
tags: [idea, groovy]
---

### Connecting to Databases

IntelliJ IDEA has most of the features of DataGrip built-in for managing databases, which can be found in the sidebar, Connect to Database.

![idea-database-groovy-01](https://uposs.justokay.cn/images/idea/idea-database-groovy-01.png)

### Locating Groovy script directories

IntelliJ IDEA already has several built-in scripts for generating model classes, we can create our own scripts according to our needs.

Right-click on any table and select Script Extensions - go to Scripts directory.

![idea-database-groovy-02](https://uposs.justokay.cn/images/idea/idea-database-groovy-02.png)

Click on `Go To Scripts Directory` and you will see the built-in scripts, double click on them to edit or add them.

![idea-database-groovy-03](https://uposs.justokay.cn/images/idea/idea-database-groovy-03.png)

### Writing Groovy script files

The following script is based on `MyBatis-Plus + Lombok`:

```groovy
import com.intellij.database.model.DasTable
import com.intellij.database.util.Case
import com.intellij.database.util.DasUtil

/*
 * Available context bindings:
 *   SELECTION   Iterable<DasObject>
 *   PROJECT     project
 *   FILES       files helper
 *
 * Tips: the script does  not support the composite primary key
 */
packageName = ""
existsPrimaryKey = false;
primaryKeyAutoGenerated = false;

typeMapping = [
        (~/(?i)tinyint|bool|boolean|bit/)        : "Boolean",
        (~/(?i)bigint/)                          : "Long",
        (~/(?i)smallint|mediumint|int|integer/)  : "Integer",
        (~/(?i)float/)                           : "Float",
        (~/(?i)double/)                          : "Double",
        (~/(?i)decimal/)                         : "BigDecimal",
        (~/(?i)date|datetime|timestamp|time/)    : "Date",
        (~/(?i)char|varchar|text|json/)          : "String",
        (~/(?i)blob|binary|bfile|clob|raw|image/): "InputStream",
        (~/^(?i)/)                               : "String"
]

FILES.chooseDirectoryAndSave("Choose directory", "Choose where to store generated files") { dir ->
    SELECTION.filter { it instanceof DasTable }.each { generate(it, dir) }
}

def generate(table, dir) {
    def className = javaName(table.getName(), true)
    def fields = calcFields(table)
    packageName = getPackageName(dir)
    new File(dir, className + ".java").withPrintWriter("UTF-8") { out -> generate(out, className, fields, table) }
}

def generate(out, className, fields, table) {
    out.println "package $packageName"
    out.println ""

    Set types = new HashSet()

    fields.each() {
        types.add(it.type)
    }

    // In the order of IDEA formatting
    if (primaryKeyAutoGenerated) {
        out.println "import com.baomidou.mybatisplus.annotation.IdType;"
    }
    out.println "import com.baomidou.mybatisplus.annotation.TableField;"
    if (existsPrimaryKey) {
        out.println "import com.baomidou.mybatisplus.annotation.TableId;"
    }
    out.println "import com.baomidou.mybatisplus.annotation.TableName;"
    if (types.contains("InputStream")) {
        out.println "import java.io.InputStream;"
    }
    if (types.contains("BigDecimal")) {
        out.println "import java.math.BigDecimal;"
    }
    if (types.contains("Date")) {
        out.println "import java.util.Date;"
    }
    out.println "import lombok.Getter;"
    out.println "import lombok.Setter;"
    out.println "import lombok.ToString;"

    out.println ""
    out.println "@Getter"
    out.println "@Setter"
    out.println "@ToString"
    out.println "@TableName(\"" + table.getName() + "\")"
    out.print "public class $className {"
    out.println ""
    fields.each { it ->

        out.println ""
        // Print comments
        if (isNotEmpty(it.comment)) {
            out.println "    /**"
            out.println "     * ${it.comment.toString()}"
            out.println "     */"
        }

        // Print annotations
        it.annos.each { anno ->
            out.println "    ${anno}"
        }

        out.println "    private ${it.type} ${it.name};"
    }
    out.println "}"
}

def calcFields(table) {
    DasUtil.getColumns(table).reduce([]) { fields, col ->
        def spec = Case.LOWER.apply(col.getDataType().getSpecification())
        def typeStr = typeMapping.find { p, t -> p.matcher(spec).find() }.value
        def annos = []
        if (DasUtil.isPrimary(col)) {
            existsPrimaryKey = true

            if (DasUtil.isAutoGenerated(col)) {
                primaryKeyAutoGenerated = true

                annos += ["@TableId(value = \"" + col.getName() + "\", type = IdType.AUTO)"]
            } else {
                annos += ["@TableId(\"" + col.getName() + "\")"]
            }
        } else {
            annos += ["@TableField(\"" + col.getName() + "\")"]
        }
        fields += [[
                           name   : javaName(col.getName(), false),
                           type   : typeStr,
                           annos  : annos,
                           comment: col.getComment()
                   ]]
    }
}

def javaName(str, capitalize) {
    str = str.startsWith("is_") ? str.substring(3) : str
    def s = com.intellij.psi.codeStyle.NameUtil.splitNameIntoWords(str)
            .collect { Case.LOWER.apply(it).capitalize() }
            .join("")
            .replaceAll(/[^\p{javaJavaIdentifierPart}[_]]/, "_")
    capitalize || s.length() == 1 ? s : Case.LOWER.apply(s[0]) + s[1..-1]
}

static def getPackageName(dir) {
    return dir.toString()
            .replaceAll("\\\\", ".")
            .replaceAll("/", ".")
            .replaceAll("^.*src(\\.main\\.java\\.)?", "") + ";"
}

static def isNotEmpty(content) {
    return content != null && content.toString().trim().length() > 0
}
```
